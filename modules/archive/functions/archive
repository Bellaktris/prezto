#!/usr/bin/env zsh

(( $# < 2 )) && \
  print "No input files provided" && return 0

[[ -f "${@[-1]}" ]] && \
  print "File exists already" && return 0


(( $+commands[pbzip2] )) && local _bzip2_bin='pbzip2' \
  || { echo "Warning: Install pbzip2"; local _bzip2_bin='bzip2' }

(( $+commands[pigz] )) && local _gzip_bin='pigz' \
  || { echo "Warning: Install pigz"; local _gzip_bin='gzip' }

case ${@[-1]} in

  (*.tar.lzma|*.tlz) tar -cvf ${@[-1]} --lzma ${@[1, -2]} && return 0;;

  (*.tar.xz|*.txz) tar -cv ${@[1, -2]} | xz -c -9 > ${@[-1]} && return 0;;

  (*.tar.bz|*.tbz|*.tbz2) tar -cv ${@[1, -2]} | \
     ${_bzip2_bin} -c -9 -n > ${@[-1]}  && return 0;;

  (*.tar.gz|*.tgz) tar -cv ${@[1, -2]} | \
     ${_gzip_bin} -c -9 -n > ${@[-1]} && return 0;;

  (*.rar) rar a -r ${@[-1]} ${@[1, -2]} && return 0;;

  (*.7z) 7z -mx=9 -mmt=8 a ${@[-1]} ${@[1, -2]} && return 0;;

  (*.zip|*.jar) zip -9 -r ${@[-1]} ${@[1, -2]} && return 0;;

  (*.tar) tar -cpvf ${@[-1]} ${@[1, -2]} && return 0;;

  (*.gz) ${_gzip_bin} -r -c -9 -n ${@[1, -2]} > ${@[-1]} && return 0;;

  (*.bz|*.bz2) ${_bzip2_bin} -r -c -9    ${@[1, -2]} > ${@[-1]} && return 0;;
esac

print "Given archive name is of unknown type, unfortunately ;("
